var egoX = 0;
var screenX = 0;

# A little function that only changes the X value of the screen
# and saves it to a variable to be used by other functions
sub setSD (x) {
	screenX = x;
	setSceneDimensions (x, 480);
}

var offset = 0;

sub scroll () {
	loop {
		egoX = getObjectX (ego);
		# Get the x offset for redrawing screen regions
		# This will give them the appearance of being fixed to the screen
		if (screenX > 640) {
			if (egoX > 320 && egoX < screenX-320) {
				offset = egoX-320;
			} else if (egoX >= screenX-320) {
				offset = screenX-640;
			} else {
				offset = 0;
			}
		} else {
			offset = 0;
		}
		# Hide the screen regions
		hideInterfaceSR ();
		aimCamera (egoX, 0);
		# Show the screen regions again
		showInterfaceSR ();
		pause(1);
	}
}

# Fading scripts

sub fadeIn () {
	transitionMode (FADE);
	for (var a = 0; a < 256; a += 20) {
		transitionLevel (a);
		pause (1);
	}
	transitionLevel (256);
}

sub fadeOut () {
	transitionMode (FADE);
	for (var a = 255; a > 0; a -= 20) {
		transitionLevel (a);
		pause (1);
	}
	transitionLevel (0);
}

# Script so the player can stop cutscenes by pressing the ESC button,
# then go on to the next scene.
# (see also keyboard.slu)

var cutsceneStart;
var cutsceneStop;
var cutscenePlaying;

sub cutscene (beginning, end) {
	cutsceneStart = beginning;
	cutsceneStop = end;
	cutscenePlaying = TRUE;
	cutsceneStart ();
	cutscenePlaying = FALSE;
	cutsceneStop ();
}